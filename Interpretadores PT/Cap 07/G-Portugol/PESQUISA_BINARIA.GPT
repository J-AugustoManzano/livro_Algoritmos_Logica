// Devido aos problemas de tratamento de dados "literais" no processamento
// de ordenação o programa funciona precariamente. Com dados eminentemente numéricos
// o programa funciona adequadamente.
algoritmo PESQUISA_BINARIA;
variáveis
  NOME : matriz[10] de literais;
  I, J, COMECO, FINAL, MEIO : inteiro;
  PESQ, RESP, X : literal;
  ACHA : lógico;
fim-variáveis
início

  para I de 1 até 10 passo 1 faça
    NOME[I - 1] := leia(); 
  fim-para

  // *** início trecho de classificação ***

  para I de 1 até 9 passo 1 faça
    para J de I + 1 até 10 passo 1 faça
      se (NOME[I - 1] > NOME[J - 1]) então
        X := NOME[I - 1];
        NOME[I - 1] := NOME[J - 1];
        NOME[J - 1] := X;
      fim-se
    fim-para 
  fim-para

  // *** fim trecho de classificação ***

  // *** início trecho de pesquisa binária ***

  RESP := "SIM";
  enquanto (RESP = "SIM") faça
    imprima("Entre o nome a ser pesquisado: ");
    PESQ := leia(); 
    COMECO := 1;
    FINAL := 10;
    ACHA := falso;
    enquanto (COMECO <= FINAL) e (ACHA = falso) faça
      MEIO := (COMECO + FINAL) / 2;
      se (PESQ = NOME[MEIO - 1]) então
        ACHA := verdadeiro;
      senão
        se (PESQ < NOME[MEIO - 1]) então
          FINAL := MEIO - 1;
        senão
          COMECO := MEIO + 1;
        fim-se
      fim-se
    fim-enquanto
    se (ACHA = verdadeiro) então
      imprima(PESQ, " foi localizado na posicao ", MEIO);
    senão
      imprima(PESQ, " nao foi localizado");
    fim-se
    imprima("Deseja continuar? (SIM/NAO): "); 
    RESP := leia();
  fim-enquanto

  // *** fim do trecho de pesquisa binária ***

fim

