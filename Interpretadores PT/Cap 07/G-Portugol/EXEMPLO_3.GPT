algoritmo EXEMPLO_3;
variáveis
  A : matriz[15] de inteiros;
  B : matriz[35] de inteiros;
  C : matriz[50] de inteiros;
  RESP : literal;
  ACHA : lógico;
  I, J, PESQ, COMECO, FINAL, MEIO, X : inteiro;
fim-variáveis
início

  // Trecho de entrada de dados da matriz A

  para I de 1 até 15 passo 1 faça
    A[I - 1] := leia(); 
  fim-para

  // Trecho de entrada de dados da matriz B

  para I de 1 até 35 passo 1 faça
    B[I - 1] := leia(); 
  fim-para

  // Trecho de junção da matriz C

  para I de 1 até 50 passo 1 faça
    se (I <= 15) então
      C[I - 1] := A[I - 1];
    senão
      C[I - 1] := B[(I - 1) - 15];
    fim-se
  fim-para

  // Trecho de ordenação da matriz C

  para I de 1 até 49 passo 1 faça
    para J de I + 1 até 50 passo 1 faça
      se (C[I - 1] > C[J - 1]) então
        X := C[I - 1];
        C[I - 1] := C[J - 1];
        C[J - 1] := X;
      fim-se
    fim-para
  fim-para

  // Trecho de pesquisa binária

  RESP := "SIM";
  enquanto (RESP = "SIM") faça
    imprima("Entre o nome a ser pesquisado: ");
    PESQ := leia(); 
    COMECO := 1;
    FINAL := 50;
    ACHA := falso;
    enquanto (COMECO <= FINAL) e (ACHA = falso) faça
      MEIO := (COMECO + FINAL) / 2;
      se (PESQ = C[MEIO - 1]) então
        ACHA := verdadeiro;
      senão
        se (PESQ < C[MEIO - 1]) então
          FINAL := MEIO - 1;
        senão
          COMECO := MEIO + 1;
        fim-se
      fim-se
    fim-enquanto
    se (ACHA = verdadeiro) então
      imprima(PESQ, " foi localizado na posicao ", MEIO);
    senão
      imprima(PESQ, " nao foi localizado");
    fim-se
    imprima("Deseja continuar? (SIM/NAO): "); 
    RESP := leia();
  fim-enquanto

fim

