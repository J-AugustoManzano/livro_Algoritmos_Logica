algoritmo PESQUISA_SEQUENCIAL;
variáveis
  NOME : matriz[10] de literais;
  I : inteiro;
  PESQ, RESP : literal;
  ACHA : lógico;
fim-variáveis
início

  para I de 1 até 10 passo 1 faça
    NOME[I - 1] := leia(); 
  fim-para

  // *** início do trecho de pesquisa sequencial ***

  RESP := "SIM";
  enquanto (RESP = "SIM") faça
    imprima("Entre o nome a ser pesquisado: ");
    PESQ := leia(); 
    I := 1;
    ACHA := falso;
    enquanto (I <= 10) e (ACHA = falso) faça
      se (PESQ = NOME[I - 1]) então
        ACHA := verdadeiro;
      senão
        I := I + 1;
      fim-se
    fim-enquanto
    se (ACHA = verdadeiro) então
      imprima(PESQ, " foi localizado na posicao ", I);
    senão
      imprima(PESQ, " nao foi localizado");
    fim-se
    imprima("Deseja continuar? (SIM/NAO): "); 
    RESP := leia();
  fim-enquanto

  // *** fim do trecho de pesquisa sequencial ***

fim

