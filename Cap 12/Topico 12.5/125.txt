Material do tópico 12.5.
========================


------------------
- Encapsulamento -
------------------

programa ENCAPSULAMENTO

tipo 

  CLS_ALUNO = classe 
                seção_pública
                  NOME : cadeia
                  função CMÉDIA : real
                  função PEGANOTA(POS : inteiro) : real 
                  função PEGAMÉDIA : real
                  procedimento PÕENOTA(NT : real, POS : inteiro)
                seção_privada
                  NOTAS : conjunto[1..4] de real
                  MÉDIA : real
              fim_classe

  função CLS_ALUNO..CMÉDIA : real
  var
    I : inteiro
    SOMA : real
  início
    SOMA ← 0
    para I de 1 até 4 passo 1 faça
      SOMA ← SOMA + ALUNO.NOTAS[I]
    fim_para
    MÉDIA ← SOMA / 4
    CMÉDIA ← MÉDIA
  fim

  função CLS_ALUNO..PEGANOTA(POS : inteiro) : real
  início
    PEGANOTA ← NOTAS[POS]
  fim

  função CLS_ALUNO..PEGAMÉDIA : real
  início
    PEGAMÉDIA ← CMÉDIA()
  fim

  procedimento CLS_ALUNO..PÕENOTA(NT : real, POS : inteiro)
  início
    NOTAS[POS] ← NT
  fim

objeto
  ALUNO : cls_aluno

var
  I : inteiro
  ENTRANOTA : real

início

  // *** Trecho de entrada dos dados ***

  escreva "Informe o nome: " 
  leia ALUNO.NOME
  escreva "Informe as notas: "
  para I de 1 até 4 passo 1 faça
    escreva I, "a. nota: " 
    leia ENTRANOTA
    ALUNO.PÕENOTA(ENTRANOTA, I)
  fim_para

  // *** Trecho de saída dos dados ***

  escreva "Nome: ", ALUNO.NOME
  para I de 1 até 4 passo 1 faça
    escreva I, "a. nota: ", ALUNO.PEGANOTA(I)
  fim_para
  escreva "Média: ", ALUNO.PEGAMÉDIA()

fim
