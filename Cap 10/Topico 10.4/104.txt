Material do tópico 10.4.
========================

---------------------------------------------
- Sub-rotina: Procedimento - Calculadora V1 -
---------------------------------------------

programa CALCULADORA_V1

  // *** Trecho dos módulos para efetivação dos cálculos ***

  procedimento ROTSOMA
  var
    R1, A1, B1 : real
  início
    escreva "Rotina de Adição"  
    escreva "Entre o 1o. valor: " leia A1
    escreva "Entre o 2o. valor: " leia B1
    R1 ← A1 + B1
    escreva "O resultado da operação equivale a: ", R1
  fim

  procedimento ROTSUBTRAÇÃO
  var
    R2, A2, B2 : real
  início
    escreva "Rotina de Subtração"  
    escreva "Entre o 1o. valor: " leia A2
    escreva "Entre o 2o. valor: " leia B2
    R2 ← A2 – B2
    escreva "O resultado da operação equivale a: ", R2
  fim

  procedimento ROTMULTIPLICAÇÃO
  var
    R3, A3, B3 : real
  início
    escreva "Rotina de Multiplicação"  
    escreva "Entre o 1o. valor: " leia A3
    escreva "Entre o 2o. valor: " leia B3
    R3 ← A3 * B3
    escreva "O resultado da operação equivale a: ", R3
  fim

  procedimento ROTDIVISÃO
  var
    R4, A4, B4 : real
  início
    escreva "Rotina de Divisão"  
    escreva "Entre o 1o. valor: " leia A4
    escreva "Entre o 2o. valor: " leia B4
    se (B4 = 0) então
      escreva "O resultado da operação equivale a: ERRO"
    senão
      R4 ← A4 / B4
      escreva "O resultado da operação equivale a: ", R4
    fim_se
  fim

// *** Trecho da parte principal do programa ***

var
  OPÇÃO : inteiro

início
  OPÇÃO ← 0
  enquanto (OPÇÃO <> 5) faça
    escreva "[1] – Adição"
    escreva "[2] – Subtração"
    escreva "[3] – Multiplicação"
    escreva "[4] – Divisão"
    escreva "[5] – Fim de Programa"
    escreva "Escolha uma opção: " 
    leia OPÇÃO
    se (OPÇÃO <> 5) então
       caso OPÇÃO
         seja 1 faça ROTSOMA
         seja 2 faça ROTSUBTRAÇÃO
         seja 3 faça ROTMULTIPLICAÇÃO
         seja 4 faça ROTDIVISÃO
       senão 
         escreva "Opção inválida – Tente novamente"
       fim_caso
    fim_se
  fim_enquanto
fim









